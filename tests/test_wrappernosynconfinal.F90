! (C) Copyright 2022- ECMWF.
! (C) Copyright 2022- Meteo-France.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

PROGRAM TEST_WRAPPERNOSYNCONFINAL

USE FIELD_MODULE
USE FIELD_FACTORY_MODULE
USE FIELD_ACCESS_MODULE
USE PARKIND1
USE FIELD_ABORT_MODULE
IMPLICIT NONE

LOGICAL :: LLSYNC_ON_FINAL  (2) = [.FALSE., .TRUE.]
LOGICAL :: LLINITIALIZED    (2) = [.FALSE., .TRUE.]

INTEGER (KIND=JPIM) :: JSOF, JSOC, JINIT

DO JINIT = 1, 2
DO JSOF = 1, 2
DO JSOC = 1, 2

  PRINT *, " SYNC_ON_FINAL = ", LLSYNC_ON_FINAL (JSOF), " INITIALIZED = ", LLINITIALIZED (JSOC), " JINIT = ", JINIT

  CALL TEST_FIELD
#ifdef HAVE_FIELD_GANG
  CALL TEST_GANG
#endif

ENDDO
ENDDO
ENDDO

CONTAINS

SUBROUTINE TEST_FIELD

CLASS(FIELD_2RB), POINTER :: W => NULL()
REAL(KIND=JPRB), ALLOCATABLE :: D(:,:), ZH (:,:)
REAL(KIND=JPRB), POINTER :: DD(:,:), DH (:,:)


REAL(KIND=JPRB) :: ZVAL1, ZVAL2
INTEGER(KIND=JPIM) :: IDIM1, IDIM2

IF (LLINITIALIZED (JSOC)) THEN
  ZVAL1 =  7
  ZVAL2 = 22
  IDIM1 = 10
  IDIM2 =  4
ELSE
  ZVAL1 = 77
  ZVAL2 =  2
  IDIM1 =  8
  IDIM2 = 15
ENDIF

ALLOCATE(D(IDIM1,IDIM2), ZH (IDIM1,IDIM2))

IF (JINIT == 1) THEN
  D=ZVAL1
ENDIF

CALL FIELD_NEW (W, DATA=D, SYNC_ON_FINAL=LLSYNC_ON_FINAL (JSOF), &
              & INITIALIZED=LLINITIALIZED (JSOC))

IF (JINIT == 2) THEN
  DH => GET_HOST_DATA_RDWR (W)
  DH=ZVAL1
ENDIF

DD => GET_DEVICE_DATA_RDWR (W)

IF ((LLINITIALIZED (JSOC) .OR. JINIT == 2) .NEQV. (W%STATS%TRANSFER_CPU_TO_GPU == 1)) THEN
  CALL FIELD_ABORT ('A TRANSFER WAS EXPECTED')
ENDIF

#ifdef OMPGPU
!$omp target map(to:DD) map(from:ZH)
#else
!$acc serial present (DD) copyout (ZH)
#endif
ZH(:,:) = DD(:,:)
#ifdef OMPGPU
!$omp end target
#else
!$acc end serial
#endif

IF (LLINITIALIZED (JSOC) .OR. JINIT == 2) THEN
  IF (ANY (ZH /= ZVAL1)) THEN
    WRITE (*, *) ZH
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF
ELSE
  IF (ALL (ZH == ZVAL1)) THEN
    WRITE (*, *) ZH
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF
ENDIF

#ifdef OMPGPU
!$omp target map(to:DD)
#else
!$acc serial present (DD)
#endif
DD(:,:) = ZVAL2
#ifdef OMPGPU
!$omp end target
#else
!$acc end serial
#endif

CALL FIELD_DELETE (W)

IF (LLSYNC_ON_FINAL (JSOF)) THEN

  IF (ANY (D /= ZVAL2)) THEN
    WRITE (*, *) D
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF

ELSE

  IF (ANY (D /= ZVAL1)) THEN
    WRITE (*, *) D
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF

ENDIF

DEALLOCATE (D)

END SUBROUTINE

#ifdef HAVE_FIELD_GANG
SUBROUTINE TEST_GANG

CLASS(FIELD_4RD), POINTER :: YLF4
TYPE(FIELD_3RD_PTR), ALLOCATABLE :: YLF3L (:)

REAL (KIND=JPRD), ALLOCATABLE :: ZDATA4 (:,:,:,:), ZH (:,:,:,:)
REAL (KIND=JPRD), POINTER :: ZD4 (:,:,:,:), ZD3 (:,:,:), ZH4 (:,:,:,:), ZH3 (:,:,:)

REAL (KIND=JPRD) :: ZVAL1, ZVAL2, ZVAL3
INTEGER (KIND=JPIM) :: IDIM1, IDIM2, IDIM3, IDIM4
LOGICAL :: LLERROR

IF (LLINITIALIZED (JSOC)) THEN
  IDIM1 =  8
  IDIM2 =  2
  IDIM3 =  3
  IDIM4 =  4
  ZVAL1 = 123._JPRD
  ZVAL2 = 456._JPRD
  ZVAL3 = 789._JPRD
ELSE
  IDIM1 =  9
  IDIM2 =  7
  IDIM3 =  3
  IDIM4 = 11
  ZVAL1 = 321._JPRD
  ZVAL2 = 654._JPRD
  ZVAL3 = 987._JPRD
ENDIF

ALLOCATE (ZDATA4 (IDIM1,IDIM2,IDIM3,IDIM4), ZH (IDIM1,IDIM2,IDIM3,IDIM4))

IF (JINIT == 1) THEN
  ZDATA4 = ZVAL1
ENDIF

CALL FIELD_NEW (YLF4, CHILDREN=YLF3L, PERSISTENT=.TRUE., DATA=ZDATA4, &
    & SYNC_ON_FINAL=LLSYNC_ON_FINAL (JSOF), INITIALIZED=LLINITIALIZED (JSOC))

IF (JINIT == 2) THEN
  ZH4 => GET_HOST_DATA_RDWR (YLF4)
  ZH4 = ZVAL1
ENDIF

ZD4 => GET_DEVICE_DATA_RDWR (YLF4)

IF ((LLINITIALIZED (JSOC) .OR. JINIT == 2) .NEQV. (YLF4%STATS%TRANSFER_CPU_TO_GPU == 1)) THEN
  CALL FIELD_ABORT ('A TRANSFER WAS EXPECTED')
ENDIF

#ifdef OMPGPU
!$omp target map(to:ZD4) map(from:ZH)
#else
!$acc serial present (ZD4) copyout (ZH)
#endif
ZH(:,:,:,:) = ZD4(:,:,:,:)
#ifdef OMPGPU
!$omp end target
#else
!$acc end serial
#endif

IF (LLINITIALIZED (JSOC) .OR. JINIT == 2) THEN
  IF (ANY (ZH /= ZVAL1)) THEN
    WRITE (*, *) ZH
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF
ELSE
  IF (ALL (ZH == ZVAL1)) THEN
    WRITE (*, *) ZH
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF
ENDIF

#ifdef OMPGPU
!$omp target map(to:ZD4)
#else
!$acc serial present (ZD4)
#endif
ZD4(:,:,:,:) = ZVAL2
#ifdef OMPGPU
!$omp end target
#else
!$acc end serial
#endif

ZD3 => GET_DEVICE_DATA_RDWR (YLF3L (1)%PTR)

#ifdef OMPGPU
!$omp target map(to:ZD3)
#else
!$acc serial present (ZD3)
#endif
ZD3(:,:,:) = ZVAL3
#ifdef OMPGPU
!$omp end target
#else
!$acc end serial
#endif

DEALLOCATE (YLF3L)
CALL FIELD_DELETE (YLF4)

IF (LLSYNC_ON_FINAL (JSOF)) THEN

  IF (ALL (ZDATA4 == ZVAL3)) THEN
    WRITE (*, *) ZDATA4 
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF

ELSE

  IF (ANY (ZDATA4 /= ZVAL1)) THEN
    WRITE (*, *) ZDATA4 
    CALL FIELD_ABORT ('UNEXPECTED VALUES')
  ENDIF

ENDIF

DEALLOCATE (ZDATA4)

END SUBROUTINE
#endif

END PROGRAM 
